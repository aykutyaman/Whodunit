buildscript {
    repositories {
        mavenCentral()
        maven {
            name = "forge"
            url = "http://files.minecraftforge.net/maven"
        }
        maven {
            name = "sonatype"
            url = "https://oss.sonatype.org/content/repositories/snapshots/"
        }
    }
    dependencies {
        if (minecraft_version == '1.6.4')
            classpath 'net.minecraftforge.gradle:ForgeGradle:1.0-SNAPSHOT'
        else
            classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-SNAPSHOT'
    }
}

apply plugin: 'forge'

archivesBaseName = "whodunit"
group = "openmods.whodunit"
ext.mod_version = "0.1"

version = "${mod_version}-${minecraft_version}"

if (minecraft_version == "1.6.4")
    ext.forge_version = '9.11.1.964' 
else if (minecraft_version == "1.7.2")
    ext.forge_version = '10.12.1.1082'
    
minecraft {
    version = minecraft_version + "-" + forge_version
    assetDir = "run/assets"
    
    replaceIn "openmods/whodunit/Mod.java"
    replace "@VERSION@", mod_version
}

sourceSets {
    main {
        java {
            srcDir "src/main/java-${minecraft_version}"
            srcDir "src/main/java"
        }
    }
}

jar {
    manifest {
        attributes("FMLCorePlugin": "openmods.whodunit.Plugin") 
    }
}

task deobfJar(type: Jar) {
    from sourceSets.main.output
    classifier = 'deobf'
}

artifacts {
    archives deobfJar
} 